# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=fbida
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.14
pkgrel=1
pkgdesc="Lossless transformation of JPEG images, does not ignore EXIF data - exiftran (mingw-w64)"
arch=('any')
url="https://www.kraxel.org/blog/linux/fbida/"
license=('GPL2')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-meson")
depends=("${MINGW_PACKAGE_PREFIX}-libexif"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-pixman"
         "${MINGW_PACKAGE_PREFIX}-poppler"
         "${MINGW_PACKAGE_PREFIX}-freetype")
options=('strip' 'staticlibs')
source=(https://www.kraxel.org/releases/fbida/fbida-${pkgver}.tar.gz{,.asc})
sha256sums=('95b7c01556cb6ef9819f358b314ddfeb8a4cbe862b521a3ed62f03d163154438'
            'SKIP')
validpgpkeys=('A0328CFFB93A17A79901FE7D4CB6D8EED3E87138')

build() {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p "${srcdir}"/build-${MINGW_CHOST} && cd "${srcdir}"/build-${MINGW_CHOST}

  meson \
    --default-library=both \
    ../${_realname}-${pkgver}

  ninja
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  DESTDIR="${pkgdir}${MINGW_PREFIX}" ninja install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
