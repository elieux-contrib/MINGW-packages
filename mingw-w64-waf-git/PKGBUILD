# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: David Macek <david.macek.0@gmail.com>

_realname=waf

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=1282.82743d7
pkgrel=1
pkgdesc="General-purpose build system modelled after Scons (mingw-w64)"
arch=('any')
license=('BSD')
url="http://code.google.com/p/waf/"
makedepends=("setconf")
depends=("${MINGW_PACKAGE_PREFIX}-python3")
options=('!emptydirs')
source=("git+https://code.google.com/p/waf/"
        waflib.patch)
sha256sums=('SKIP'
            '2961b7ad38aee5589de0704b83b3234c29d6d48b61e212b9641b1232e8eed540')

pkgver() {
  cd "${_realname}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_realname}"
  #patch -p1 -i ${srcdir}/waflib.patch
  # Python 3 fix
  sed -i 's#/usr/bin/env python#python3#' waf-light
  sed -i 's#/usr/bin/env python#python3#' wscript
}

build() {
  cd "${_realname}"
  plain "Configure ..."
  python3 ./waf-light configure --prefix=${MINGW_PREFIX}
  plain "Make waf ..."
  python3 ./waf-light --make-waf
  # Extracting license
  head -n 30 waf | tail -n 25 > LICENSE
}

package() {
  cd "${_realname}"

  python3 ./waf-light install -f --destdir="$pkgdir" \
    --tools='compat,compat15,ocaml,go,cython,scala,erlang,cuda,gcj,boost,pep8,eclipse'

  install -Dm755 waf "${pkgdir}${MINGW_PREFIX}/bin/waf"

  # Force the generation of .waf.admin files
  cd demos/c
  "${pkgdir}${MINGW_PREFIX}/bin/waf" configure build >& /dev/null
  cd ../..

  # Fix weird directory placement (FS#38216, FS#38300)
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/waf"
  mv "${pkgdir}${MINGW_PREFIX}/bin/.waf3-$pkgver-"* "${pkgdir}${MINGW_PREFIX}/lib/waf/"
  setconf "${pkgdir}${MINGW_PREFIX}/bin/waf" base '"${MINGW_PREFIX}/lib/waf"'

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

}
