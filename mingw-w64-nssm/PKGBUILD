# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=nssm
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.23
pkgrel=1
pkgdesc="The Non-Sucking Service Manager (mingw-w64)"
url='http://nssm.cc/'
license=('PD')
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("nssm::git://git.nssm.cc/nssm/nssm.git#tag=v${pkgver}"
        "0001-Fixes.patch"
        "makefile"
        # generated by: `mc -u -U messages.mc -r . -h .`
        "messages.h"
        "messages.rc"
        "MSG00409.bin"
        "MSG0040C.bin"
        "MSG00410.bin")
sha1sums=('SKIP'
          '6d0e6780ad3c87b4c960ff29733416e0662cc783'
          '96c7a1767cb31baa5015b12556a4f3379bc37923'
          '95ddcd27e986b63e78c96fc8e9e848791fc65ebe'
          '6bc6a87bc8935d3771b9069d5c36adf4aaee2926'
          'b433018594bbe3c1319231378a8b64b6f9529ac6'
          '4445b49ef296d6bdd8736d354984e6800d4a0122'
          'b08dc0fca63626804ddb30b9838d53a7d77e46ce')

prepare() {
  cd "${srcdir}"

  pushd nssm
  cp ../*.{h,rc,bin} ./
  cp ../makefile ./
  patch -p1 -i "${srcdir}"/0001-Fixes.patch
  ./version.cmd
  popd

  mkdir -p "${srcdir}"/build-${CARCH}
  cp -r "${srcdir}"/nssm/* "${srcdir}"/build-${CARCH}
}

build() {
  cd "${srcdir}"/build-${CARCH}
  export CXXFLAGS+=" -Wno-write-strings -DWIN32 -DNDEBUG -D_CONSOLE -fpermissive -fno-inline"
  export LDFLAGS+=" -lcomdlg32 -lshlwapi"
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  cp nssm.exe "${pkgdir}${MINGW_PREFIX}/bin/"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc/nssm"
  cp *.txt "${pkgdir}${MINGW_PREFIX}/share/doc/nssm/"
}
